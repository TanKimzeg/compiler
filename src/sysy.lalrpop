use compiler::ast::*;
use koopa::ir::Type;
// lalrpop 里的约定
grammar;

// 约束 lexer 的行为
match {
  // 跳过空白符和注释
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*[^/]*\*/" => {},
  // 剩下的情况采用默认方式处理
  _
}

pub Module: Module = <units: CompUnit*> => Module{ <> };

CompUnit: CompUnit = {
  FuncDef => CompUnit::Func(<>),
  Decl => CompUnit::Decl(<>),
};

FuncDef: FuncDef = {
  <head: FuncHead> ")" <block: Block> => {
    FuncDef { func_type: head.0, id: head.1, params: Vec::new(), block }
  },
  <head: FuncHead> <p0: FuncFParam> <mut ps: ("," <FuncFParam>)*> ")" <block: Block> => {
    ps.insert(0, p0);
    FuncDef { func_type: head.0, id: head.1, params: ps, block }
  }
}
FuncHead: (Type, String) = {
  "int" <id: Ident> "("  => (Type::get_i32(), id),
  "void" <id: Ident> "(" => (Type::get_unit(), id),
}

FuncFParam: (String, Type) = {
  "int" <id: Ident> => (id, Type::get_i32()),
}


Block: Block = "{" <items: BlockItem*> "}" => Block::new(items);

BlockItem: BlockItem = {
  <stmt: Stmt> => BlockItem::Stmt(<>),
  <decl: Decl> => BlockItem::Decl(<>),
};

Stmt: Stmt = {
  MatchedStmt => <>,
  UnmatchedStmt => <>,
};

MatchedStmt: Stmt = {
  "return" <exp: Exp?> ";" => Stmt::Ret(<>),
  <lval: LVal> "=" <exp: Exp> ";" => Stmt::Assign(<>),
  Block => Stmt::Block(<>),
  <exp: Exp?> ";" => Stmt::Exp(<>),
  "if" "(" <exp: Exp> ")" <then_m: MatchedStmt> "else" <else_m: MatchedStmt> =>
    Stmt::Cond(exp, Box::new(then_m), Some(Box::new(else_m))),
  "while" "(" <exp: Exp> ")" <stmt: MatchedStmt> =>
    Stmt::While(exp, Box::new(stmt)),
  "break" ";" => Stmt::Break,
  "continue" ";" => Stmt::Continue,
};

UnmatchedStmt: Stmt = {
  "if" "(" <exp: Exp> ")" <then_branch: Stmt> =>
    Stmt::Cond(exp, Box::new(then_branch), None),
  "if" "(" <exp: Exp> ")" <then_m: MatchedStmt> "else" <else_u: UnmatchedStmt> =>
    Stmt::Cond(exp, Box::new(then_m), Some(Box::new(else_u))),
  "while" "(" <exp: Exp> ")" <stmt: UnmatchedStmt> =>
    Stmt::While(exp, Box::new(stmt)),
};

// ---------------------- Exp ------------------------
Exp: Exp = <add_exp: LOrExp> => Exp::LOrExp(<>);

LVal: LVal = {
  <id: Ident> => LVal::Ident(id),
  <id: Ident> "[" <exp: Exp> "]" => LVal::ArrayElem(id, Box::new(exp)),
};

LOrExp: LOrExp = {
  <land_exp: LAndExp> => LOrExp::Single(<>),
  <lor_exp: LOrExp> "||" <land_exp: LAndExp> =>
    LOrExp::Binary(Box::new(
      Binary { lhs: Box::new(lor_exp), op: BinOp::LOr, rhs: Box::new(land_exp) }
    ))
};

LAndExp: LAndExp = {
  <eq_exp: EqExp> => LAndExp::Single(<>),
  <land_exp: LAndExp> "&&" <eq_exp: EqExp> =>
    LAndExp::Binary(Box::new(
      Binary { lhs: Box::new(land_exp), op: BinOp::LAnd, rhs: Box::new(eq_exp) }
    )),
};

EqExp: EqExp = {
  <rel_exp: RelExp> => EqExp::Single(<>),
  <eq_exp: EqExp> "==" <rel_exp: RelExp> =>
    EqExp::Binary(Box::new(
      Binary { lhs: Box::new(eq_exp), op: BinOp::Eq, rhs: Box::new(rel_exp) }
    )),
  <eq_exp: EqExp> "!=" <rel_exp: RelExp> =>
    EqExp::Binary(Box::new(
      Binary { lhs: Box::new(eq_exp), op: BinOp::NEq, rhs: Box::new(rel_exp) }
    )),
};

RelExp: RelExp = {
  <add_exp: AddExp> => RelExp::Single(<>),
  <rel_exp: RelExp> "<" <add_exp: AddExp> =>
    RelExp::Binary(Box::new(
      Binary { lhs: Box::new(rel_exp), op: BinOp::LT, rhs: Box::new(add_exp) }
    )),
  <rel_exp: RelExp> ">" <add_exp: AddExp> =>
    RelExp::Binary(Box::new(
      Binary { lhs: Box::new(rel_exp), op: BinOp::GT, rhs: Box::new(add_exp) }
    )),
  <rel_exp: RelExp> "<=" <add_exp: AddExp> =>
    RelExp::Binary(Box::new(
      Binary { lhs: Box::new(rel_exp), op: BinOp::LE, rhs: Box::new(add_exp) }
    )),
  <rel_exp: RelExp> ">=" <add_exp: AddExp> =>
    RelExp::Binary(Box::new(
      Binary { lhs: Box::new(rel_exp), op: BinOp::GE, rhs: Box::new(add_exp) }
    )),
};

AddExp: AddExp = {
  <mul_exp: MulExp> => AddExp::Single(<>),
  <add_exp: AddExp> "+" <mul_exp: MulExp> =>
    AddExp::Binary(Box::new(
      Binary { lhs: Box::new(add_exp), op: BinOp::Add, rhs: Box::new(mul_exp) }
    )),
  <add_exp: AddExp> "-" <mul_exp: MulExp> =>
    AddExp::Binary(Box::new(
      Binary { lhs: Box::new(add_exp), op: BinOp::Sub, rhs: Box::new(mul_exp) }
    )),
};

MulExp: MulExp = {
  <unary_exp: UnaryExp> => MulExp::Single(<>),
  <mul_exp: MulExp> "*" <unary_exp: UnaryExp> => 
    MulExp::Binary(Box::new(
      Binary { lhs: Box::new(mul_exp), op: BinOp::Mul, rhs: Box::new(unary_exp) }
    )),
  <mul_exp: MulExp> "/" <unary_exp: UnaryExp> => 
    MulExp::Binary(Box::new(
      Binary { lhs: Box::new(mul_exp), op: BinOp::Div, rhs: Box::new(unary_exp) }
    )),
  <mul_exp: MulExp> "%" <unary_exp: UnaryExp> => 
    MulExp::Binary(Box::new(
      Binary { lhs: Box::new(mul_exp), op: BinOp::Mod, rhs: Box::new(unary_exp) }
    )),
};

UnaryExp: UnaryExp = {
  <pexp: PrimaryExp> => UnaryExp::PExp(<>),
  <u_op: UnaryOP> <u_exp: UnaryExp> => UnaryExp::OpExp(u_op, Box::new(u_exp)),
  // f()
  <id: Ident> "(" ")" => UnaryExp::FuncCall(id, Vec::new()),
  // f(a, b, ...)
  <id: Ident> "(" <a0: Exp> <mut args: ("," <Exp>)*> ")" => {
    args.insert(0, a0);
    UnaryExp::FuncCall(id, args)
  },
};

PrimaryExp: PrimaryExp = {
  "(" <exp: Exp> ")" => PrimaryExp::Exp(Box::new(exp)),
  <num: Number> => PrimaryExp::Number(num),
  LVal => PrimaryExp::LVal(<>),
};

Number: i32 = <num: IntConst> => <>;

UnaryOP: UnaryOP = {
  "+" => UnaryOP::Plus,
  "-" => UnaryOP::Minus,
  "!" => UnaryOP::Not,
};
// ---------------------- Exp ------------------------


// ---------------------- Decl -------------------------
Decl: Decl = {
  ConstDecl => Decl::ConstDecl(<>),
  VarDecl => Decl::VarDecl(<>),
};

ConstDecl: ConstDecl = {
  "const" "int" <d0: ConstDef> <mut defs: ("," <ConstDef>)*> ";" => {
    defs.insert(0, d0);
    ConstDecl { ty: Type::get_i32(), defs }
  },
};

ConstDef: ConstDef = {
  <id: Ident> <index: ("[" <Exp> "]")?> "=" <init: ConstInit> =>
    match index {
      None => ConstDef::ConstExp(id, init),
      Some(index) => ConstDef::ConstArray(id, index, init),
    },
};

ConstInit: ConstInit = {
  Exp => ConstInit::ConstExp(<>),
  "{" "}" => ConstInit::ConstArray(Vec::new()),
  "{" <e0: Exp> <mut es: ("," <Exp>)*> "}" => {
    es.insert(0, e0);
    ConstInit::ConstArray(es)
  },
};

VarDecl: VarDecl = {
  "int" <d0: VarDef> <mut defs: ("," <VarDef>)*> ";" => {
    defs.insert(0, d0);
    VarDecl { ty: Type::get_i32(), defs }
  },
};

VarDef: VarDef = {
  <id: Ident> <index: ("[" <Exp> "]")?> <init: ("=" <ValInit>)?> =>
    match index {
      None => VarDef::Var(id, init),
      Some(index) => VarDef::ArrayVar(id, index, init)
    },
};

ValInit: ValInit = {
  Exp => ValInit::Exp(<>),
  "{" <e0: Exp> <mut es: ("," <Exp>)*> "}" => {
    es.insert(0, e0);
    ValInit::Array(es)
  },
};
// ---------------------- Decl -------------------------

// 如果匹配到标识符, 就返回这个字符串
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<> , 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<> , 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}
